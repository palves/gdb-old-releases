# Commands covered:  load
#
# This file contains a collection of tests for one or more of the Tcl
# built-in commands.  Sourcing this file into Tcl runs the tests and
# generates output for errors.  No output means no errors were found.
#
# Copyright (c) 1995 Sun Microsystems, Inc.
#
# See the file "license.terms" for information on usage and redistribution
# of this file, and for a DISCLAIMER OF ALL WARRANTIES.
#

if {[string compare test [info procs test]] == 1} then {source defs}

# Figure out what extension is used for shared libraries on this
# platform.

set ext [info sharedlibextension]
set testDir [file dirname [info nameofexecutable]]/dltest
if ![file readable $testDir/pkg1$ext] {
    puts "libraries in $testDir haven't been compiled: skipping tests"
    return
}

if {[info loaded] != ""} {
    puts "load tests have already been run once:  skipping (can't rerun)"
    return
}

test load-1.1 {basic errors} {
    list [catch {load a} msg] $msg
} {1 {wrong # args: should be "load fileName packageName ?interp?"}}
test load-1.2 {basic errors} {
    list [catch {load a b c d} msg] $msg
} {1 {wrong # args: should be "load fileName packageName ?interp?"}}
test load-1.3 {basic errors} {
    list [catch {load a b foobar} msg] $msg
} {1 {couldn't find slave interpreter named "foobar"}}

test load-2.1 {basic loading, with init proc name conversion} {
    load $testDir/pkg1$ext pKg1
    list [pkg1_eq abc def] [info commands pkg1_*]
} {0 {pkg1_eq pkg1_quote}}
interp create -safe child
test load-2.2 {loading into a safe interpreter} {
    load $testDir/pkg2$ext Pkg2 child
    list [child eval pkg2_sub 44 13] [catch {child eval pkg2_unsafe} msg] $msg \
	    [catch {pkg2_sub 12 10} msg2] $msg2
} {31 1 {invalid command name "pkg2_unsafe"} 1 {invalid command name "pkg2_sub"}}
test load-2.3 {loading with no _Init procedure} {
    list [catch {load $testDir/pkg3$ext foo} msg] $msg
} {1 {package "Foo" has no Foo_Init procedure}}
test load-2.4 {loading with no _SafeInit procedure} {
    list [catch {load $testDir/pkg4$ext foo child} msg] $msg
} {1 {package "Foo" can't be used in a safe interpreter: no Foo_SafeInit procedure}}

test load-3.1 {reloading package into same interpreter} {
    list [catch {load $testDir/pkg1$ext pkg1} msg] $msg
} {0 {}}
test load-3.2 {reloading package into same interpreter} {
    list [catch {load $testDir/pkg1$ext pkg2} msg] $msg
} "1 {file \"$testDir/pkg1$ext\" is already loaded for package \"Pkg1\"}"

if $doNonPortableTests {
    # On some platforms, like SunOS 4.1.3, these tests can't be run because
    # they cause the process to exit.

    test load-4.1 {errors loading file} {
	catch {load foo foo}
    } {1}
    test load-4.2 {errors loading file} {
	list [catch {load $testDir/pkg5$ext pkg5}] [info commands pkg5_*]
    } {1 {}}
}

test load-5.1 {TclGetLoadedPackages command} {
    info loaded
} "{$testDir/pkg4$ext Foo} {$testDir/pkg3$ext Foo} {$testDir/pkg2$ext Pkg2} {$testDir/pkg1$ext Pkg1}"
test load-5.2 {TclGetLoadedPackages command} {
    list [catch {info loaded gorp} msg] $msg
} {1 {couldn't find slave interpreter named "gorp"}}
test load-5.3 {TclGetLoadedPackages command} {
    list [info loaded {}] [info loaded child]
} "{{$testDir/pkg1$ext Pkg1}} {{$testDir/pkg2$ext Pkg2}}"
test load-5.4 {TclGetLoadedPackages command} {
    load $testDir/pkg2$ext pkg2
    list [info loaded {}] [lsort [info commands pkg2_*]]
} "{{$testDir/pkg2$ext Pkg2} {$testDir/pkg1$ext Pkg1}} {pkg2_sub pkg2_unsafe}"
interp delete child
