dnl	This file is an input file used by the GNU "autoconf" program to
dnl	generate the file "configure", which is run during Tk installation
dnl	to configure the system for the local environment.
AC_INIT(tk.h)
AC_PROG_INSTALL
AC_PROG_RANLIB
CC=${CC-cc}
AC_SUBST(CC)
AC_HEADER_CHECK(unistd.h, AC_DEFINE(HAVE_UNISTD_H))

#--------------------------------------------------------------------
#	Include sys/select.h if it exists and if it supplies things
#	that appear to be useful and aren't already in sys/types.h.
#	This appears to be true only on the RS/6000 under AIX.  Some
#	systems like OSF/1 have a sys/select.h that's of no use, and
#	other systems like SCO UNIX have a sys/select.h that's
#	pernicious.  If "fd_set" isn't defined anywhere then set a
#	special flag.
#--------------------------------------------------------------------

AC_MSG_CHECKING(checking for fd_set)
AC_TRY_LINK([#include <sys/types.h>], [fd_set readMask, writeMask;], 
	fd_in_types_h=yes, fd_in_types_h=no)
if test "$fd_in_types_h" = "yes" ; then
	AC_MSG_RESULT(yes)
else
	AC_HEADER_EGREP(fd_mask, sys/select.h, fd_in_select_h=yes,
		fd_in_select_h=no)

	if test "$fd_in_select_h" = "yes" ; then
		AC_DEFINE(HAVE_SYS_SELECT_H)
		AC_MSG_RESULT(yes)
	else
		AC_DEFINE(NO_FD_SET)
		AC_MSG_RESULT(no)
	fi
fi

#--------------------------------------------------------------------
#	Supply a substitute for stdlib.h if it doesn't define strtol,
#	strtoul, or strtod (which it doesn't in some versions of SunOS).
#--------------------------------------------------------------------

echo checking for proper stdlib.h
AC_HEADER_EGREP(strtol, stdlib.h, tk_stdlib=1, tk_stdlib=0)
AC_HEADER_EGREP(strtoul, stdlib.h, , tk_stdlib=0)
AC_HEADER_EGREP(strtod, stdlib.h, , tk_stdlib=0)
if test $tk_stdlib = 1; then
    AC_DEFINE(HAVE_STDLIB_H)
fi

#--------------------------------------------------------------------
#	Check for various typedefs and provide substitutes if
#	they don't exist.
#--------------------------------------------------------------------

AC_MODE_T
AC_PID_T
AC_SIZE_T
AC_UID_T

AC_PATH_X

if test "x$x_includes" != "x" ; then
	XINCLUDES="-I$x_includes"
else
	XINCLUDES=""
fi

if test "x$x_libraries" != "x" ; then
	XLIBSW="-L$x_libraries -lX11"
else
	XLIBSW="-lX11"
fi

AC_SUBST(XLIBSW)
AC_SUBST(XINCLUDES)

#--------------------------------------------------------------------
#	Check for the existence of various libraries.  The order here
#	is important, so that then end up in the right order in the
#	command line generated by Make.
#--------------------------------------------------------------------

AC_CHECK_LIB(Xbsd, main, [LIBS="$LIBS -lXbsd"])
AC_CHECK_LIB(socket, main, [LIBS="$LIBS -lsocket"])
AC_CHECK_LIB(nsl, main, [LIBS="$LIBS -lnsl"])

AC_OUTPUT(Makefile)
